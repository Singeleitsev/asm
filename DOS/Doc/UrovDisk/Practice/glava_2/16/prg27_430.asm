;----------------------------------------------------------
;prg27_430 - программа на ассемблере линейного поиска в массиве (вариант 2).
;Вход: mas[n] - неупорядоченная последовательность двоичных значений длиной n+1; k - искомое значение
;Выход: i - позиция в mas[n] (0<i<n-1), соответствующая найденному символу.
;----------------------------------------------------------
masm
model small
.data
;задаем массив
mas	db	50h,08h,52h,06h,90h,17h,89h,27h,65h,42h,15h,51h,61h,67h,76h,70h
n=$-mas
	db	0h	;дополнительный элемент для барьера
k	db	15h
.stack	256
.486
.code
main:
;НАЧ_ПРОГ
	mov	dx,@data
	mov	ds,dx
;i:=0; mas[n]:=k	//установить барьер
	xor	si,si	;i(si):=0
	mov	al,k
	mov	mas[n],al	;установим барьер
;s2:
;ECЛИ k==mas[i] ТО ПЕРЕЙТИ_НА _exit
s2:	cmp	al,mas[si]
	je	ok
;i:=i+1; ПЕРЕЙТИ_НА s2
	inc	si
	jmp	s2
;_exit:
ok:
;ECЛИ i>n ТО ПЕРЕЙТИ_НА exit_error
	cmp	si,n
	je	exit_error
;вставить реакцию на удачный результат поиска
	jmp	exit
exit_error:
;реакция на неудачный результат поиска
exit:
	mov	ax,4c00h
	int	21h
end	main
