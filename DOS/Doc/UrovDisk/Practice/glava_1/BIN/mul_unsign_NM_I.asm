;----------------------------------------------------------
;mul_unsign_NM_I.asm - программа mul_unsign_NM_I умножения N-байтного числа на число размером M байт
;(порядок - младший байт по младшему адресу (Intel))
;Вход: U и V - множители размерностью N и M байт соответственно; b=256 - размерность ;машинного слова.
;Выход: W - произведение размерностью N+M байт.
;----------------------------------------------------------
MASM
MODEL small
STACK	256
.data
;значения в U и V нужно внести
U	db	?	;множитель 1 размерностью N байт
i=$-U
V	db	?	;множитель 2 размерностью M байт
j=$-V
len_product=$-U
W	db	len_product dup (0)	;len_product=N+M
k	db	0	;перенос 0 Ј k < 255
b	dw	100h	;размер машинного слова
.code
mul_unsign_NM_I	proc
;НАЧ_ПРОГ
;m1
;ДЛЯ j:= 0 ДО M-1 //J изменяется в диапазоне 0..M-1
	xor	bx,bx
	mov	cx,j
m2:
;НАЧ_БЛОК_1
	push	cx	;вложенные циклы
;//проверка на равенство нулю очередного элемента множителя (не обязательно)
;ЕСЛИ v[j]==0 ТО ПЕРЕЙТИ_НА m6
	cmp	v[bx],0
	je	m6
;m3
;k:=0; i:=n-1 //i изменяется в диапазоне 0..N-1
;ДЛЯ i:= 0 ДО N-1
	xor	si,si
	mov	cx,i
	mov	k,0
m4:
;НАЧ_БЛОК_2
;//перемножаем очередные элементы множителей
;temp_word:=u[i]*v[j]+w[i+j]+k
	mov	al,u[si]
	mul	byte ptr v[bx]
	xor	dx,dx
	mov	dl,w[bx+si]
	add	ax,dx
	xor	dx,dx
	mov	dl,k
	add	ax,dx	;t=(ax) - временная переменная
;w[i+j]:=temp_word MOD b //остаток от деления temp_word\b -> w[i+j]
;k:=temp_word\b //целая часть частного temp_word\b -> k
	push	dx
	xor	dx,dx
	div	b	;t mod b
	mov	ah,dl
	pop	dx
	mov	k,al
	mov	w[bx+si],ah
;m5
	inc	si
	loop	m4
;КОН_БЛОК_2
;w[i+j]:=k
	mov	al,k
	mov	w[bx+si],al
;m6:
m6:
	inc	bx
	pop	cx
	loop	m2
;КОН_БЛОК_1
	ret
;КОН_ПРОГ
mul_unsign_NM_I	endp
main:
	mov	dx,@data
	mov	ds,dx
	call	mul_unsign_NM_I
	mov	ax,4c00h
	int	21h
end	main
