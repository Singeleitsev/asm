;----------------------------------------------------------
;rand_add.asm - аддитивный генератор случайных чисел.
;Вход: ;X0, a, c, m ;
;случайная последовательность длиной 55 значений, получаемая с помощью программы генерации высокослучайных двоичных наборов (см. rand_mix_cong_1.asm);
;N=700 - количество элементов в последовательности + 1;
;L=7 - значение степени m=27.
;Выход: dl - значение очередного случайного числа.
;----------------------------------------------------------
masm
model small
.486
.data
N=700	;количество элементов в последовательности + 1
L=7	;L - значение степени m=27
m	db	128	;128=27
mm	dw	256	;256=28
a	db	9
c	dw	3
;массив значений х (начальное значение x=3) длиной N+1
x	db	3, N dup (0ffh)
.stack	256
.486
.code
main:
	mov	dx,@data
	mov	ds,dx
	xor	dx,dx
;----------------------------------------------------------
;далее фрагменты из rand_mix_cong_1.asm.
;----------------------------------------------------------
	xor	si,si
	mov	ecx,54	;счетчик цикла для формирования начальной последовательности
;первое число в последовательности x=3
	mov	al,x
cycl:
;вычисляем очередное случайное число X=(a*X) mod m
	mul	a	;a*x в ah:al
	add	ax,c
	shrd	ax,ax,L	;L - значение степени m=27
	xor	al,al
	rol	ax,L	;в al случайное число
;вывод в массив x и файл - командная строка rand_mult_cong.exe > p.txt
	inc	si
	mov	x[si],al
	mov	dl,al
	mov	ah,02
	int	21h
	loop	cycl
;далее продолжаем формирование случайной последовательности, но уже аддитивным методом
	mov	ecx,N-55
cycl2:	inc	si
	xor	dx,dx
	mov	al,x[si-24]
	mov	dl,x[si-55]
	add	ax,dx
	xor	dx,dx
	div	mm
	mov	x[si],dl
		mov	ah,02
	int	21h
	loop	cycl2
exit:
	mov	ax,4c00h
	int	21h
end	main
