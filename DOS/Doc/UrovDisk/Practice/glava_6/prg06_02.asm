;----------------------------------------------------------
;prg06_02.asm - программа преобразования целого десятичного числа в символьном виде из диапазона 0..4294967295 в эквивалентное двоичное представление.
;Вход: ввод с клавиатуры числа в десятичной системе счисления (не более 10 цифр).
;Выход: двоичное число-результат преобразования в регистре EAX.
;----------------------------------------------------------
masm
model small
.data
string	db	12 dup (0)	;максимальное число состоит из 10 цифр (12 - с учетом 0d0ah)
len_string=$-string
ten	dd	10
string_e	db	0dh,0ah,"Результат вышел за границы операнда !!!$"
adr_string_e	dd	string_e
.stack	256
.486
.code
main:
	mov	ax,@data	;адрес сегмента данных в регистр ax
	mov	ds,ax	;ax в ds
	xor	edx,edx
;---------вводим с клавиатуры---------------------------------------------
	mov	bx,0	;стандартный дескриптор - клавиатура
	mov	cx,len_string
	lea	dx,string	;формируем указатель на строку string
	mov	ah,3fh	;номер функции DOS
	int	21h
	jc	exit	;переход в случае ошибки
;в регистре AL - количество действительно введенных десятичных цифр
	mov	ecx,eax
	sub	ecx,2	; корректируем счетчик цикла (чтобы не учитывать 0d0ah, вводимые 3fh)
	jecxz	exit	;число не было введено
	dec	ecx	;не умножать на 10 последнюю цифру числа
	jecxz	m2	;введено однозначное число
	lea	si,string	;формируем указатель на строку string
	xor	eax,eax	;eax:=0
m1:
	xor	edx,edx
	mov	dl,[si]
	and	dl,0fh
	add	eax,edx
	mul	ten
	jc	exit_e	;результат вышел за границы операнда
	inc	si
	loop	m1
m2:
	mov	dl,[si]
	and	dl,0fh	;преобразуем ASCII->BCD
	add	eax,edx	;результат преобразования в регистре eax
	jnc	exit	;результат вышел за границы операнда
exit_e:	;выводим строку string_e на экран
	lds	dx,adr_string_e	;адрес строки в DS:DX
	mov	ah,09h
	int	21h
exit:
;выход из программы
	mov	ax,4c00h	;пересылка 4c00h в регистр ax
	int	21h	;вызов прерывания с номером 21h
end	main		;конец программы с точкой входа main
